apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

defaultTasks 'clean', 'build'

group = 'com.soundcloud'
version = project.properties.get('buildVersion') ?: '3.0-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.netflix.hystrix:hystrix-core:1.4.23'
  compile 'io.prometheus:simpleclient:0.0.13'
}

ext.sharedManifest = manifest {
  attributes('Implementation-Title': project.name, 'Implementation-Version': version)
}

jar {
  manifest = project.manifest {
    from sharedManifest
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
  manifest = project.manifest {
    from sharedManifest
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
  manifest = project.manifest {
    from sharedManifest
  }
}

task mavenPom(dependsOn: classes) {
  ext.pomPath = "$libsDir/pom.xml"
  ext.pomFile = file(pomPath)
  doLast {
    pom {
      project {
        name 'prometheus-hystrix-metrics-publisher'
        description 'This is an implementation of a HystrixMetricsPublisher that publishes metrics using the Prometheus java client.'
        developers {
          developer {
            id 'tomcz'
            name 'Tom Czarniecki'
            organization = 'SoundCloud'
            organizationUrl 'https://soundcloud.com'
          }
        }
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
        url 'https://github.com/soundcloud/prometheus-hystrix'
        scm {
          connection 'scm:git:git@github.com:soundcloud/prometheus-hystrix.git'
          url 'scm:git:git@github.com:soundcloud/prometheus-hystrix.git'
          developerConnection 'scm:git:git@github.com:soundcloud/prometheus-hystrix.git'
        }
        issueManagement {
          system 'github'
          url 'https://github.com/soundcloud/prometheus-hystrix/issues'
        }
      }
    }.writeTo(pomPath)
  }
}

artifacts {
  archives file: mavenPom.pomFile, builtBy: mavenPom
  archives sourcesJar
  archives javadocJar
  archives jar
}

signing {
  sign configurations.archives
}

signArchives.onlyIf { project.hasProperty('releaseBuild') }

task releaseJar(type: Jar, dependsOn: assemble) {
  destinationDir = distsDir
  classifier = 'release'
  from libsDir
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: project.properties.get('mavenUploadURL') ?: "file://tmp/repo/")
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.1'
}
